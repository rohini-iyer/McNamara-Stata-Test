* Code written by - Rohini Venkitaraman Iyer
* Purpose - Robert S McNamara fellowship Software test 1
* Date - 30th March 2024
* This .do file performs all the cleaning and analysis

* Inside the zipped folders, there are 2 folders - data (which contains the input files) and output (that contains all the tables, graph, and dataset generated by this code).
* There is also a word document which includes all the explanations and comments to the questions (which are commented out in this .do file).

cap log close
cap cmdlog close
set more off
clear all

* *************************** Replace with your file path here to point to the folder where you downloaded the zipped folder called "Rohini_Venkitaraman_Iyer_Stata.zip" ***************************
global path "/Users/rohiniiyer/Library/CloudStorage/OneDrive-HarvardUniversity/Resume/International Organizations/World Bank/McNamara"

global data "$path/Rohini_Venkitaraman_Iyer_Stata/data"

global output "$path/Rohini_Venkitaraman_Iyer_Stata/output"

* Importing data (Stata files)
use "$data/GEM_Baseline.dta", clear

* *************************** Task 1 - Data cleaning ***************************

describe

* Checking for duplicates in Household_ID
destring HHID, replace

duplicates tag HHID, generate(dup) 
tab dup, m // There are 3 observations with missing HHID and 3 observations with duplicate HHID

duplicates tag, generate(dup_all)
tab dup_all, m // The 3 observations with duplicate HHID that are identical across all variables

// Dropping all the duplicates identified above because the ones missing HHID cannot be matched to treatment status and for the other 3, we are retaining 1 set of their data
drop if HHID==.
duplicates drop HHID, force
drop dup dup_all

* Merging w/ treatment status data (needed for graph creation later)
merge 1:1 HHID using "$data/GEM_Treatment_Status.dta"
drop if _merge!=3
drop _merge

* Making data numeric and well labelled
destring q122_father_attend_school, replace
label def yn 0 "NO" 1 "YES"
label val q122_father_attend_school yn
           
label val q130_a_husband yn
label val q130_b_boyfriend yn
label val q130_c_father yn
label val q130_d_mother yn
label val q130_e_stepfather yn
label val q130_f_stepmother yn
label val q130_g_father_in_law yn
label val q130_h_mother_in_law yn
label val q130_i_own_children yn
label val q130_j_grandparents yn
label val q130_k_brothers yn
label val q130_l_sisters yn

destring w20_hours_unpaid_job99, replace
destring s02_cash_savings, replace

tab w02_paid_in_cash_job1, m
replace w02_paid_in_cash_job1="N33" if w02_paid_in_cash_job1=="FOOD HAWKER"
replace w02_paid_in_cash_job1="R23" if w02_paid_in_cash_job1=="HOT WAITERS"
tab w02_paid_in_cash_job1, m
replace w02_paid_in_cash_job3="." if w02_paid_in_cash_job3==".."
// I leave the three job variables in string form because they contain non-numeric characters from the work codes

label def treat 0 "control" 1 "treatment"
label val treatment treat
label var treatment "Treatment Status"
label var HHID "Household ID"

// Question w16_unpaid_job99 is a select all that apply kind of multiple choice question. 
// Ideally, I would have broken this variable down into a series of 0/1 type dummy variable - one for each option.
// And then created a separate "rowsum" column that adds all the 1s for the different unpaid tasks that the women undertook over the past 7 days.
// However, since that is time consuming, I am proceeding further with the remaining test and will revisit this at the end if I have time.
// This is also something I would consult with my supervisor regarding

* *************************** Task 2A - Table ***************************

* Converting savings from Kenyan Shillings to US Dollars
gen cash_savings_usd = s02_cash_savings/135
gen jewellery_savings_usd = s04_jewellery_savings_value/135
label var cash_savings_usd "Cash Savings"
label var jewellery_savings_usd "Jewellery Savings"

* Creating a total_savings variable that sums cash and jewellery savings for each household
egen total_savings_usd=rowtotal(cash_savings_usd jewellery_savings_usd)
replace total_savings_usd = . if cash_savings_usd==. & jewellery_savings_usd==.
label var total_savings_usd "Total Savings"
// There is 1 household that has missing values for both. Hence I modified total_savings to show a missing value for this household rather than 0 because there are 16 other households that actually report 0 savings.

eststo clear
estpost summarize cash_savings_usd jewellery_savings_usd total_savings_usd, detail
esttab using "$output/savings_table.rtf", replace ///
    cells("count(fmt(a2)) mean p50 sd min max") label ///
	collabels("Number of Households" "Mean" "Median" "SD" "Min" "Max") ///
    title("Table 1: Descriptive Statistics (in USD)") nomtitle nonumber noobs
// NOTE: Number of households represents the NON-MISSING number of observations for each variable

* *************************** Task 2B - Graph ***************************

// NOTE - For this graph, job types 1 to 3 are PAID jobs since BEGINNING OF 2012 while job type 4 is UNPAID work in the LAST 7 DAYS.
// I make this note and proceed due to shortage of time. Under normal circumstances, I would discuss this decision with my Supervisor. 

preserve

* 1. Creating a job type level dataset
rename w16_unpaid_job99 w02_paid_in_cash_job4
keep HHID treatment w02_paid_in_cash_job1 w02_paid_in_cash_job2 w02_paid_in_cash_job3 w02_paid_in_cash_job4 w07_hours_job1 w07_hours_job2 w07_hours_job3 w20_hours_unpaid_job99 

reshape long w02_paid_in_cash_job, i(HHID) j(job)

gen hours= w07_hours_job1 if job==1
replace hours= w07_hours_job2 if job==2
replace hours= w07_hours_job3 if job==3
replace hours= w20_hours_unpaid_job99 if job==4

drop w07_hours_job1 w07_hours_job2 w07_hours_job3 w20_hours_unpaid_job99
rename w02_paid_in_cash_job work_code

label def job_type 1 "Paid job 1" 2 "Paid job 2" 3 "Paid job 3" 4 "Unpaid work"
label val job job_type

order HHID job work_code hours treatment

replace hours=. if hours<0 // Changing 4 hour values to missing since negative hours don't make sense and likely point to reporting error
save "$output/GEM_Job_Type.dta", replace

egen sum_hours = total(hours), by(job treatment) 
tab job treatment, summarize(sum_hours) // Creating the graph output in table format to confirm final numbers

* 2. Creating the graph
gen treat_hours=hours if treatment==1
gen control_hours=hours if treatment==0
		
graph bar (sum) treat_hours (sum) control_hours, over(job) ///
	ytitle(Total number of hours) ytitle(, size(medsmall)) ylabel(#5, labsize(small)) ymtick(##4, labsize(small)) ///
	title("Total number of hours worked by job-type", size(medium)) ///
	subtitle("(by treatment status)", size(small)) ///
	note("Note - Paid jobs 1, 2, and 3 report data of most recent jobs held since beginning of 2012. However, unpaid work is reported for the past 7 days before the date of the survey.", size(tiny)) ///
	legend(on order(1 "Treatment" 2 "Control"))
graph save "$output/Graph1.gph", replace

restore

// From this graph, women in the control group seem to engage in higher number of “paid” labor hours as indicated in the difference in bar heights particularly for jobs 2 and 3.

* *************************** Task 3 - Regression ***************************

egen hh_members=rowtotal(q130_a_husband q130_b_boyfriend q130_c_father q130_d_mother q130_e_stepfather q130_f_stepmother q130_g_father_in_law q130_h_mother_in_law q130_i_own_children q130_j_grandparents q130_k_brothers q130_l_sisters)
label var hh_members "Total HH members eating from one pot"

egen total_hours_paid=rowtotal(w07_hours_job1 w07_hours_job2 w07_hours_job3)
label var total_hours_paid "Total Hours worked in paid jobs"

* Regression equation (with clustering at household level)
reg total_savings_usd q102_age q105_attend_school q120_a_vocational_training q134_i_water_piped q134_a_electricity q134_c_television q134_l_sewing_machine w02_paid_in_cash w20_hours_unpaid_job99 m901_b_currently_married m912_a_spouse_attend_school m912_spouse_years_education treatment total_hours_paid hh_members, vce(cluster HHID)

* The above regression uses total savings in USD as the outcome variable and factors like age of the respondent, whether they attended school or received vocation training (before this intervention), ///
* their household characteristics like having piped water, and assets like sewing machine, electricity, television, whether the house is rented, and how many household members (eating from the same pot) ///
* whether the women received cash/kind payment for any work they did and the total no. of paid and unpaid hours of labor they engaged in, ///
* their current marital status and their spouses' education and their treatment status in the experiment as independent variables to understand what affects household savings.

* Based on the above regression, the women's age, whether they went to school, whether their spouse went to school, and whether they own a sewing machine are factors that positively and ///
* significantly influence total savings. The results are significant because p < 0.05 (except for age when it i < 0.1). The regression is also clustered at the household level to account for within household correlation.

eststo clear
reg total_savings_usd q102_age q105_attend_school q120_a_vocational_training q134_i_water_piped q134_a_electricity q134_c_television q134_l_sewing_machine w02_paid_in_cash w20_hours_unpaid_job99 m901_b_currently_married m912_a_spouse_attend_school m912_spouse_years_education treatment total_hours_paid hh_members, vce(cluster HHID)
esttab using "$output/regression_table.rtf", replace ///
    cells("b se t p") label ///
	collabels("Coefficient" "Robust Standard Error" "t" "p value") ///
    title("Table 2: Regression Results")

* If I had more time -
* 1. I would format my table (like editing the independent variable names)
* 2. Give a more detailed interpretation of the results (like using the magnitudes of the significant results to comment about the dependent variable)
* 3. Spend some time performing regressions with each independent variable separately because the fact that hours of paid labor, or household members, or hours of unpaid labor are not significant predictors ///
* of household savings is puzzling and seems counterintuitive.
* 4. The sample size for regressions is only 133 which is only about 10% of the dataset. This is due to the missing values but I would spend some time figuring how to deal with them and also get my supervisor's advice on this.
* Other than the variables available in this dataset, a few other indicators that I would be interested in observing as factors that influece household savings from the broader survey would be -
* Migration indicators - like if they have ever lived outside Nairobi, especially in an urban setting | where they accumulate their savings, whether they have a bank account, age at start of marriage, no. of children, etc.
	 
* *************************** Optional task 4 - Randomization evaluation ***************************
* 1. I would create balance tables that compare basic sociodemographics like age, education, marital status, no. of children, no. of household members, household savings, assets, hours spent on paid v/s unpaid labor, etc. ///
* for the treatment and the control groups to check if the randomization has resulted in 2 groups that are similar on all other observable and unobservable indicators prior to the beginning of the intervention. ///
* This would help isolate and attribute any differences between the groups post intervention to the treatment alone.

* 2. I would start with some summary statistics like mean, SD, proportions for each characteristic broken down by treatment status. Then I would create balance tables using t-tests to compare sample means of the two groups on the factors listed in the previous point. 
* Code - ttest var, by(treatment)
* The expectation is that the t-test results for each variable would NOT BE SIGNIFICANT indicating that treatment and control means are not significantly different from each other for that variable.

* 3. Variables that I would use would be similar to the ones I used and outlined in the regression section.
